buildscript {

    repositories {
        mavenLocal()

        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '3.2.0')
    }
}

plugins {

    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'


def _version = "04-10-2016-3"
version _version
group "com.abzreporting"

task clean(type: Delete) {
    delete 'build'
}

task buildBlockly(type: Exec, dependsOn: clean) {
    workingDir file('.')
    commandLine 'python', 'build.py'
}

task archiveZip(type: Zip, dependsOn: buildBlockly) {
    baseName = "blockly-${_version}"
    destinationDir = file('build')
    ['core', 'blocks', 'i18n', 'media', 'msg'].each { v ->
        from(v) {
            into(v)
        }
    }
    from('.') {
        include 'block*.js'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifact(file("build/blockly-${version}.zip"))
        }
    }
}

artifactory {
    contextUrl = 'https://artifactory.abz-reporting.com/artifactory'
    publish {
        repository {
            repoKey = 'ext-release-local' // The Artifactory repository key to publish to
            username = "${abzLdapUser}" // The publisher user name
            password = "${abzLdapPassword}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}

artifactoryPublish.dependsOn archiveZip
